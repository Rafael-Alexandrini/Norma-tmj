// limpa variável
operation clear(A){
 1: if zero A then goto 0 else goto 2
 2: do dec A goto 1
}

// A:=A+B (soma destrutiva)
operation soma(A,B){
  1: if zero B then goto 0 else goto 2
  2: do dec B goto 3
  3: do inc A goto 1
}

// Soma: A:=A+B usando C
operation soma(A,B,C){
  1: if zero B then goto 5 else goto 2
  2: do dec  B goto 3
  3: do inc  A goto 4
  4: do inc  C goto 1
  5: if zero C then goto 0 else goto 6
  6: do dec  C goto 7
  7: do inc  B goto 5
}

// A:=B (atribuição destrutiva)
operation load(A,B){
  1: do clear(A) goto 2
  2: do soma(A,B) goto 0
}

// A:=B usando C (atribuição não-destrutiva)
operation load(A,B,C){
  1: do clear(A) goto 2
  2: do soma(A,B,C) goto 0
}

operation mult2(A,C){
  1: do load(C,A) goto 2
  2: if zero C then goto 0 else goto 3
  3: do dec C goto 4
  4: do add A 2 goto 2
}

// A = 2^B usando C,D
operation exp2(A,B,C,D){
 1: do clear(A) goto 2
 2: do inc A goto 3
 3: do load(C,B,D) goto 4
 4: if zero C then goto 0 else goto 5
 5: do mult2(A,D) goto 6
 6: do dec C goto 4
}

//teste de divisão por 2
test divBy2(A,B,C){
  1: do load(C,A,B) goto 2
  2: if zero C then goto true else goto 3
  3: do dec C goto 4
  4: if zero C then goto false else goto 5
  5: do dec C goto 2
}

// A = A/3
operation div3(A,B,C){
  1: do load(C,A,B) goto 2
  2 :do clear(A) goto 3
  3: if zero C then goto 0 else goto 4
  4: do dec C goto 5
  5: if zero C then goto 0 else goto 6
  6: do dec C goto 7
  7: if zero C then goto 0 else goto 8
  8: do dec C goto 9
  9: do inc A goto 3
}

main {
 1: do exp2(Y,X,C,D) goto 2
 2: if divBy2(X,C,D) then goto 3 else goto 4
 3: do dec Y goto 5
 4: do inc Y goto 5
 5: do div3(Y,C,D) goto 0
}